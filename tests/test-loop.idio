;;
;; Copyright (c) 2022 Ian Fitchet <idf(at)idio-lang.org>
;;
;; Licensed under the Apache License, Version 2.0 (the "License"); you
;; may not use this file except in compliance with the License.  You
;; may obtain a copy of the License at
;;
;;     http://www.apache.org/licenses/LICENSE-2.0
;;
;; Unless required by applicable law or agreed to in writing, software
;; distributed under the License is distributed on an "AS IS" BASIS,
;; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;; See the License for the specific language governing permissions and
;; limitations under the License.
;;
;;

;;
;; test-loop.idio
;;

module tests/loop

loop-test0 := Tests

i := 37					; random number

;; default loop should return (void)
test (C/for ((i 0 (1 + i))) (i lt 4) {
  i + 13
}) (void)

;; and shouldn't have altered our top-level variable
test i 37

;; break should return *value*
test (C/for ((i 0 (1 + i))) (i lt 4) {
  if (i gt 1) {
    break 99
  }
  i + 13
}) 99

;; continue should skip remaining body, here, we'll skip an uneven
;; portion of the loop (to check we haven't messed up, somehow)
n := 0
test (C/for ((i 0 (1 + i))) (i lt 6) {
  if (i lt 2) {
    (continue)
  }
  n = n + 1
}) (void)
test n 4

;; break ignores dynamic-wind
n = 0
test (C/for ((i 0 (1 + i))) (i lt 4) {
  unwind-protect {
    if (i gt 1) {
      break 99
    }
    i + 13
  } {
    n = n + 1
  }
}) 99
test n 2

;; continue ignores dynamic-wind
n = 0
test (C/for ((i 0 (1 + i))) (i lt 6) {
  unwind-protect {
    if (i gt 1) {
      (continue)
    }
    i + 13
  } {
    n = n + 1
  }
}) (void)
test n 2

Tests? (loop-test0 + 9)
